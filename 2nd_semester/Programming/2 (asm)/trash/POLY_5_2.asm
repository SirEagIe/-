TITLE POLINOM
;Программа вычисления полинома вида Y=a1*X^n+a2*X^(n-1)+...+an*X+an+1
;Входные параметры:
;коэффиценты полинома ai в массиве MAS_A
;порядок полинома N
;аргумент полинома X
;Выходные параметры: 
;вычисленное значение полинома Y
;сообщения MES_1, MES_2, MES_3, MES_Y
.MODEL SMALL	;Модель памяти ближнего типа
.DATA	;Открыть сегмент данных
	MAS_A DW -3, 9, 5, 1, 2	;Коэффиценты полинома
	N DW 4	;Порядок полинома равен 4
	X DW 10	;Аргумент полинома равен 10
	Y DW (?)	;Результат вычисления полинома
	MES_1 DB 'Overflow', 13, 10, '$'	;Сообщение MES_1
	MES_2 DB 'Result minus', 13, 10, '$'	;Сообщение MES_2
	MES_3 DB 'Result plus', 13, 10, '$'	;Сообщение MES_3
	MES_Y DB 'Y=-3*10^4+9*10^3+5*10^2+1*10^1+2*10^0', 13, 10, '$'	;Строка с полиномом
.STACK 100h	;Отвести под стек 256 байт
.Code	;Открыть сегмент кодов
Output PROC	;Начать процедуру с именем Output
	mov DX, BX	;Вывести на экран
	mov AH, 09	;строку, смещение которой находится в регистре BX,
	int 21h	;с помощью DOS
	ret	;Возврат из процедуры
Output ENDP	;Закончить процедуру с именем Output

Start:
	mov AX, @DATA	;Инициалищировать
	mov DS, AX	;сегментный регистр DS
	lea SI, MAS_A	;Загрузить в SI начальный адрес массива MAS_A
	mov CX, N	;Поместить в CX значение переменной N
	xor DX, DX	;Очистить регистр DX
	mov AX, [SI]	;Поместить в AX первый элемент элемент массива
	mov BX, offset MES_1	;Поместить в BX смещение строки MES_2
M_1:
	imul X	;Умножить значение в регистре AX на значение переменной X
	jo Result	;При условии переполнения перейти к метке Result
	inc SI	;Увеличение индекса
	inc SI	;на 2
	add AX, [SI]	;Добавить к значению AX элемент массива
	jo Result	;При условии переполнения перейти к метке Result
	loop M_1	;Уменьшить значение CX на 1, перейти к метке M_1, если значение CX не равно 0
	mov Y, AX	;Поместить значение AX в переменную Y
	cmp AX, 0	;Сравнить AX с 0
	mov BX, offset MES_3	;Если AX больше нуля поместить в BX смещение строки MES_3
	jge Result	;и перейти к метке Result
	mov BX, offset MES_2	;Иначе - поместить в BX смещение строки MES_2
	jmp Result	;и перейти к метке Result
Result:
	call Output	;Вызвать процедуру Output
	mov BX, offset MES_Y	;Поместить в BX смещение строки MES_Y
	call Output	;Вызвать процедуру Output
	mov AL, 0	;Завершить
	mov AH, 4Ch	;программу
	int 21h	;с помощью DOS
END Start	;Конец исходного модуля