TITLE GRAFIC
;Программа построения графика функции в текстовом режиме экрана
;Входные параметры:
;массив значения элементов графика MAS
.MODEL SMALL	;Модель памяти ближнего типа
.DATA	;Открыть сегмент данных
	MAS DB 0,1,5,8,9,8,5,1,0,-1,-5,-8,-9,-8,-5,-1
	FUNC DW 450,-350,0,250,375,400,420,360,250,200,150
		 DW 325,300,285,200,0,-200,-275,-250,-150,-100
		 DW -50,0,50,100,150,200
.STACK 256 (?)	;Отвести под стек 256 байт
.Code	;Открыть сегмент кодов
Start:
	mov AX, @DATA	;Инициалищировать
	mov DS, AX	;сегментный регистр DS
;------------------Задание режима экрана с очисткой------------------
	mov AH, 0	;Выбор функции задания режима экрана
	mov AL, 3	;режим видеоадаптере 80x25, 16 цветов
	int 10h	;вызов функции BIOS
;---------------Построение вертикальной оси координат----------------
	mov BH, 0	;Используем страницу видеопамяти 0
	mov DL, 5	;Координаты начальной точки X=5,
	mov DH, 1	;Y=1
met1:
	mov AH, 02h	;Выюираем функцию установки курсора
	int 10h	;Установка курсора
	mov CX, 1	;Выводим по одному символу
	mov AL, 7Ch	;Символ вертикальной чёрточки
	mov BL, 0001111b	;Атрибут: ярко белый по чёрному фону
	mov AH, 09h	;Выбираем функцию вывода символа и атрибута в позицию курсора
	int 10h	;Вывод символа
	inc DH	;Переход к координате Y+1
	cmp DH, 24	;Сравнения с нижней позицией
	jb met1	;если ниже, повторить цикл вывода символа
;--------------Построение горизонатльной оси координат---------------
	mov BH, 0	;Используем страницу видеопамяти 0
	mov DL, 5	;Координаты начальной позиции X=5,
	mov DH, 12	;Y=12
	mov AH, 02h	;Выюираем функцию установки курсора
	int 10h	;Установка курсора
	mov CX, 50	;Длина цепочки символов
	mov AL, 5Fh	;Символ горизонатльной чёрточки
	mov BL, 00001111b	;Ярко-белый по чёрному фону
	mov AH, 09h	;Выбираем функцию вывода символа и атрибута в позицию курсора
	int 10h	;Вывод символа
;------------------------Вывод точек графика-------------------------
	lea SI, MAS	;Загрузка адреса массива значений
	mov DI, 16	;Установка счётчика циклов
	mov CX, 1	;Вывод по одному символу
	mov BH, 0	;Используем страницу видеопамяти 0
	mov DL, 5	;Координаты первой точки X=5,
met2:
	mov DH, 12	;Второй - Y=12
	sub DH, [SI]	;Вычисление Y(i) в системе координат графика
	mov AH, 02h	;Установка курсора в вычисленную
	int 10h	;позицию
	mov AL, 2Ah	;Символ '*'
	mov BL, 00001100b	;Ярко красный по чёрному фону
	mov AH, 09h	;Выбираем функцию вывода символа и атрибута в позицию курсора
	int 10h	;Вывод символа
	add DL, 3	;Следующая координата по X
	inc SI	;Индекс следующего элемента массива
	dec DI	;Изменения счётчика циклов
	jnz met2	;Повторить цикл, если ZF=0
;------------------------Завершение программы------------------------
	mov AH, 00h	;пауза до
	int 16h	;нажатия клавиши
	mov AX, 4C00h	;Завершить программу
	int 21h	;с помощью DOS
END Start	;Конец исходного модуля