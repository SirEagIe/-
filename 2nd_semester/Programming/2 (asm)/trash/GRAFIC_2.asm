TITLE GRAFIC_2
;Программа построения графика функции в текстовом режиме экрана
;Входные параметры:
;массив значения элементов графика FUNC
.MODEL SMALL	;Модель памяти ближнего типа
.DATA	;Открыть сегмент данных
	MAS DB 0,1,5,8,9,8,5,1,0,-1,-5,-8,-9,-8,-5,-1
	FUNC DW 450,-350,0,250,375,400,420,360,250,200,150
		 DW 325,300,285,200,0,-200,-275,-250,-150,-100
		 DW -50,0,50,100,150,200
	M DB 50	;масштабный коэффицент
.STACK 256 (?)	;Отвести под стек 256 байт
.Code	;Открыть сегмент кодов
Start:
	mov AX, @DATA	;Инициалищировать
	mov DS, AX	;сегментный регистр DS
;------------------Задание режима экрана с очисткой------------------
	mov AH, 0	;Выбор функции задания режима экрана
	mov AL, 3	;режим видеоадаптере 80x25, 16 цветов
	int 10h	;вызов функции BIOS
;---------------Построение вертикальной оси координат----------------
	mov BH, 0	;Используем страницу видеопамяти 0
	mov DL, 40	;Координаты начальной точки X=40,
	mov DH, 1	;Y=1
met1:
	mov AH, 02h	;Выюираем функцию установки курсора
	int 10h	;Установка курсора
	mov CX, 1	;Выводим по одному символу
	mov AL, 0B3h	;Символ вертикальной чёрточки
	mov BL, 0001010b	;Атрибут: ярко зелёный по чёрному фону
	mov AH, 09h	;Выбираем функцию вывода символа и атрибута в позицию курсора
	int 10h	;Вывод символа
	inc DH	;Переход к координате Y+1
	cmp DH, 24	;Сравнения с нижней позицией
	jb met1	;если ниже, повторить цикл вывода символа
;------------------------Вывод стрелки вверх-------------------------
	mov DL, 40	;Координаты точки X=40,
	mov DH, 1	;Y=1
	mov AH, 02h	;Выюираем функцию установки курсора
	int 10h	;Установка курсора
	mov AL, 18h	;Символ стрелки вверх
	mov BL, 0001010b	;Атрибут: ярко зелёный по чёрному фону
	mov CX, 1	;Выводим по одному символу
	mov AH, 09h	;Выбираем функцию вывода символа и атрибута в позицию курсора
	int 10h	;Вывод символа
;-----------------------Вывод Y рядом с осью-------------------------
	dec DL	;Уменьшить координату столбца на единицу
	mov AH, 02h	;Выюираем функцию установки курсора
	int 10h	;Установка курсора
	mov AL, 59h	;Символ 'Y'
	mov AH, 09h	;Выбираем функцию вывода символа и атрибута в позицию курсора
	int 10h	;Вывод символа
;--------------Построение горизонатльной оси координат---------------
	mov BH, 0	;Используем страницу видеопамяти 0
	mov DL, 1	;Координаты начальной позиции X=1,
	mov DH, 12	;Y=12
	mov AH, 02h	;Выюираем функцию установки курсора
	int 10h	;Установка курсора
	mov CX, 77	;Длина цепочки символов
	mov AL, 0C4h	;Символ горизонатльной чёрточки
	mov BL, 0001010b	;Атрибут: ярко зелёный по чёрному фону
	mov AH, 09h	;Выбираем функцию вывода символа и атрибута в позицию курсора
	int 10h	;Вывод символа
;-----------------------Вывод стрелки вправо-------------------------
	mov DL, 78	;Координаты точки X=78,
	mov DH, 12	;Y=12
	mov AH, 02h	;Выюираем функцию установки курсора
	int 10h	;Установка курсора
	mov AL, 1Ah;Символ стрелки вправо
	mov BL, 0001010b	;Атрибут: ярко зелёный по чёрному фону
	mov CX, 1	;Выводим по одному символу
	mov AH, 09h	;Выбираем функцию вывода символа и атрибута в позицию курсора
	int 10h	;Вывод символа
;-----------------------Вывод X рядом с осью-------------------------
	inc DH	;Увеличить координату строки на единицу
	mov AH, 02h	;Выюираем функцию установки курсора
	int 10h	;Установка курсора
	mov AL, 58h	;Символ 'X'
	mov AH, 09h	;Выбираем функцию вывода символа и атрибута в позицию курсора
	int 10h	;Вывод символа
;------------------------Вывод точек графика-------------------------
	lea SI, FUNC	;Загрузка адреса массива значений
	mov DI, 20	;Установка счётчика циклов
	mov CX, 1	;Вывод по одному символу
	mov BH, 0	;Используем страницу видеопамяти 0
	mov DL, 14	;Координаты первой точки X=14,
met2:
	mov DH, 12	;Второй - Y=12
	mov AX, [SI]	;Помещение значния в AX
	idiv M	;Деление значения на масштабный коэффицент
	sub DH, AL	;Вычисление Y(i) в системе координат графика
	mov AH, 02h	;Установка курсора в вычисленную
	int 10h	;позицию
	mov AL, 2Ah	;Символ '*'
	mov BL, 00001001b	;Ярко синий по чёрному фону
	mov AH, 09h	;Выбираем функцию вывода символа и атрибута в позицию курсора
	int 10h	;Вывод символа
	add DL, 2	;Следующая координата по X
	inc SI	;Индекс следующего
	inc SI	;элемента массива
	dec DI	;Изменения счётчика циклов
	jnz met2	;Повторить цикл, если ZF=0
;------------------------Завершение программы------------------------
	mov AH, 00h	;пауза до
	int 16h	;нажатия клавиши
	mov AX, 4C00h	;Завершить программу
	int 21h	;с помощью DOS
END Start	;Конец исходного модуля